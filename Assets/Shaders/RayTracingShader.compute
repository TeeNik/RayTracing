// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#pragma target 3.5

#include "UnityCG.cginc" 
#include <UnityShaderVariables.cginc>
#include "RayTracingStructures.cginc"

RWTexture2D<float4> Result;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

float2 _PixelOffset;
float4 _DirectionalLight;
StructuredBuffer<Sphere> _Spheres;
float2 _Pixel;
float _Seed;

static const float PI = 3.14159265f;
static const float EPSILON = 1e-8;

StructuredBuffer<MeshObject> _MeshObjects;
StructuredBuffer<float3> _Vertices;
StructuredBuffer<int> _Indices;

StructuredBuffer<BVHNode> _Nodes;
StructuredBuffer<BVHTriangle> _Triangles;

StructuredBuffer<float2> _Uvs;
Texture2D<float4> _CubeTexture;
SamplerState sampler_CubeTexture;

UNITY_DECLARE_TEX2DARRAY(_Textures);

float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

// Random value in normal distribution (with mean=0 and sd=1)
float RandomValueNormalDistribution()
{
    // Thanks to https://stackoverflow.com/a/6178290
    float theta = 2 * 3.1415926 * rand();
    float rho = sqrt(-2 * log(rand()));
    return rho * cos(theta);
}

// Calculate a random direction
float3 RandomDirection()
{
    // Thanks to https://math.stackexchange.com/a/1585996
    float x = RandomValueNormalDistribution();
    float y = RandomValueNormalDistribution();
    float z = RandomValueNormalDistribution();
    return normalize(float3(x, y, z));
}

float3 RandomHemisphereDirection(float3 normal)
{
    float3 dir = RandomDirection();
    return dir * sign(dot(normal, dir));
}

float3x3 GetTangentSpace(float3 normal)
{
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
    {
        helper = float3(0, 0, 1);
    }
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

float3 SampleHemisphere(float3 normal, float alpha)
{
    // Sample the hemisphere, where the alpha determines the kind of the sampling
	// Uniform: 1.0f
	// Cosine:  0.5f
	// Phong:   1.0f / (alpha + 1.0f)

    float cosTheta = pow(rand(), 1 / (alpha + 1));
    float sinTheta = sqrt(max(0, 1 - cosTheta * cosTheta));
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}

void IntersectGroundPlane(Ray ray, inout RayHit hit)
{
    float t = -ray.origin.y / ray.dir.y;
    if (t > 0 && t < hit.dist)
    {
        hit.dist = t;
        hit.pos = ray.origin + t * ray.dir;
        hit.normal = float3(0, 1, 0);
        hit.material.albedo = 0.5f;
        hit.material.specular = 0.03f;
        hit.material.smoothness = 0.2;
        hit.material.emission = float3(0, 0, 0);
    }
}

void IntersectSphere(Ray ray, inout RayHit hit, Sphere sphere)
{
    float3 d = ray.origin - sphere.pos;
    float p1 = -dot(ray.dir, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
    {
        return;
    }

    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < hit.dist)
    {
        hit.dist = t;
        hit.pos = ray.origin + t * ray.dir;
        hit.normal = normalize(hit.pos - sphere.pos);
        hit.material = sphere.material;
    }
}

bool IntersectAABB(Ray ray, float3 boxMin, float3 boxMax) {
    float3 tMin = (boxMin - ray.origin) / ray.dir;
    float3 tMax = (boxMax - ray.origin) / ray.dir;
    float3 t1 = min(tMin, tMax);
    float3 t2 = max(tMin, tMax);
    float tNear = max(max(t1.x, t1.y), t1.z);
    float tFar = min(min(t2.x, t2.y), t2.z);
    return tNear <= tFar;
};

bool IntersectTriangle_MT97(Ray ray, float3 vert0, float3 vert1, float3 vert2,
    inout float t, inout float u, inout float v)
{
    float3 edge1 = vert1 - vert0;
    float3 edge2 = vert2 - vert0;

    float3 pvec = cross(ray.dir, edge2);
    float det = dot(edge1, pvec);
    if (det < EPSILON)
    {
        return false;
    }

    float inv_det = 1 / det;
    float3 tvec = ray.origin - vert0;
    u = dot(tvec, pvec) * inv_det;
    if (u < 0 || u > 1)
    {
        return false;
    }

    float3 qvec = cross(tvec, edge1);
    v = dot(ray.dir, qvec) * inv_det;
    if (v < 0 || u + v > 1)
    {
        return false;
    }

    t = dot(edge2, qvec) * inv_det;
    return true;
}

void IntersectMeshObject(Ray ray, inout RayHit hit, MeshObject mesh)
{
    if (IntersectAABB(ray, mesh.boundMin, mesh.boundMax))
    {
        uint offset = mesh.indicesOffset;
        uint count = offset + mesh.indicesCount;
        for (uint i = offset; i < count; i += 3)
        {
            float3 v0 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i]], 1))).xyz;
            float3 v1 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i + 1]], 1))).xyz;
            float3 v2 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i + 2]], 1))).xyz;

            float t, u, v;
            if (IntersectTriangle_MT97(ray, v0, v1, v2, t, u, v))
            {
                if (t > 0 && t < hit.dist)
                {
                    hit.dist = t;
                    hit.pos = ray.origin + t * ray.dir;
                    hit.normal = normalize(cross(v1 - v0, v2 - v0));
                    hit.material = mesh.material;

                    hit.uv = u * _Uvs[_Indices[i]] + v * _Uvs[_Indices[i + 1]] + (1 - u - v) * _Uvs[_Indices[i + 2]];
                }
            }
        }
    }
}

void IntersectMeshObjectBVH(Ray ray, inout RayHit hit, MeshObject mesh)
{
    BVHNode nodeStack[1];
    int stackIndex = 0;
    nodeStack[stackIndex++] = _Nodes[0];

    while (stackIndex > 0)
    {
        BVHNode node = nodeStack[--stackIndex];
        if (IntersectAABB(ray, node.BoundsMin, node.BoundsMax))
        {
            if (node.ChildIndex == 0)
            {
                for (int i = node.TriangleIndex; i < node.TriangleIndex + node.TriangleCount; i++)
                {
                    BVHTriangle tri = _Triangles[i];
                    float t, u, v;
                    if (IntersectTriangle_MT97(ray, tri.VertexA, tri.VertexB, tri.VertexC, t, u, v))
                    {
                        if (t > 0 && t < hit.dist)
                        {
                            hit.dist = t;
                            hit.pos = ray.origin + t * ray.dir;
                            hit.normal = normalize(cross(tri.VertexB - tri.VertexA, tri.VertexC - tri.VertexA));
                            hit.material = mesh.material;
                            //hit.uv = u * _Uvs[_Indices[i]] + v * _Uvs[_Indices[i + 1]] + (1 - u - v) * _Uvs[_Indices[i + 2]];
                        }
                    }
                }
            }
            else
            {
                nodeStack[stackIndex++] = _Nodes[node.ChildIndex + 1];
                nodeStack[stackIndex++] = _Nodes[node.ChildIndex + 0];
            }
        }
    }
}

float2 mod2(float2 x, float2 y)
{
    return x - y * floor(x / y);
}

float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}

float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}

float3 GetEnvironmentLight(Ray ray)
{
    float skyGradientT = pow(smoothstep(0, 0.4, ray.dir.y), 0.35);
    float groundToSkyT = smoothstep(-0.01, 0, ray.dir.y);
    float3 skyGradient = lerp(float3(1,1,1), float3(0.08,.36, .72), skyGradientT);
    float sun = pow(max(0, dot(ray.dir, _WorldSpaceLightPos0.xyz)), 500) * 200;
    // Combine ground, sky, and sun
    float3 composite = skyGradient + sun * (groundToSkyT>=1);
    return composite;
}

RayHit Trace(Ray ray)
{
    RayHit hit = CreateRayHit();
    uint count, stride;
    //IntersectGroundPlane(ray, hit);

    _Spheres.GetDimensions(count, stride);
    for (uint i = 0; i < count; ++i)
    {
        IntersectSphere(ray, hit, _Spheres[i]);
    }

    //IntersectMeshObjectBVH(ray, hit, _MeshObjects[0]);

    _MeshObjects.GetDimensions(count, stride);
    for (uint i = 0; i < count; ++i)
    {
        IntersectMeshObject(ray, hit, _MeshObjects[i]);
    }

    return hit;
}

float3 Shade(inout Ray ray)
{
    float3 result = float3(0, 0, 0);
    float fogAddition = 0;
    float3 fogColor = float3(1,1,1);

    for (int i = 0; i < 8; ++i)
    {
        RayHit hit = Trace(ray);

        if (hit.dist < 1.#INF)
        {
            Material material = hit.material;

            if (material.flag == CheckerPattern)
            {
                float2 c = mod2(floor(hit.pos.xz), 2.0);
                material.albedo = c.x == c.y ? material.albedo : material.specular;
            }
            else if (material.flag == HasTexture)
            {
                //material.albedo = _CubeTexture.SampleLevel(sampler_CubeTexture, hit.uv, 0).xyz;
                material.albedo = UNITY_SAMPLE_TEX2DARRAY_LOD(_Textures, float3(hit.uv, 0), 0).xyz;
            }
            else if (material.flag == Invisible && i == 0)
            {
                ray.origin = hit.pos + ray.dir * 0.001;
                continue;
            }

            ray.origin = hit.pos + hit.normal * 0.001;
            const float3 diffuseDir = normalize(hit.normal + RandomDirection());
            const float3 specularDir = reflect(ray.dir, hit.normal);
            const bool isSpecularBounce = material.specularChance >= rand();
            ray.dir = lerp(diffuseDir, specularDir, material.smoothness * isSpecularBounce);

            const float3 emittedLight = material.emission * material.emissionStrength;
            result += emittedLight * ray.energy;
            ray.energy *= lerp(material.albedo, material.specular, isSpecularBounce);
        }
        else
        {
            float theta = acos(ray.dir.y) / -PI;
            float pi = atan2(ray.dir.x, -ray.dir.z) / -PI * 0.5;
            //result += _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(pi, theta), 0).xyz * 0.15;
            result += GetEnvironmentLight(ray) * 0.35f * ray.energy;
            
            if (i == 0)
            {
                fogColor = GetEnvironmentLight(ray);
                fogAddition = 1;
            }
            
            break;
        }

        if (i == 0)
        {
            float depth = hit.dist;
            float fogDensity = saturate(depth * 0.10f);
            fogAddition = pow(fogDensity, 6.0f);
            //fogColor = GetEnvironmentLight(ray);

            //fogAddition = min(1.f, hit.dist / 50);
            //fogAddition = smoothstep(.3,.3, fogAddition);
            
        }

        if (!any(ray.energy))
        {
            break;
        }
    }

    float skyGradientT = ray.dir.y;
    float3 cc = float3(0,skyGradientT,0);
    //result = lerp(result, float3(.8,0,.8), fogAddition);
    //return float3(fogAddition,fogAddition,fogAddition);
    return result;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    _Pixel = id.xy;

    uint width, height;
    Result.GetDimensions(width, height);

    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2 - 1);
    Ray ray = CreateCameraRay(uv, _CameraToWorld, _CameraInverseProjection);

    float3 result = Shade(ray);
    Result[id.xy] = float4(result, 1);
}
