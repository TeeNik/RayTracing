// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

#include "RayTracingStructures.cginc"

RWTexture2D<float4> Result;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

float2 _PixelOffset;
float4 _DirectionalLight;
StructuredBuffer<Sphere> _Spheres;
float2 _Pixel;
float _Seed;

static const float PI = 3.14159265f;
static const float EPSILON = 1e-8;

StructuredBuffer<MeshObject> _MeshObjects;
StructuredBuffer<float3> _Vertices;
StructuredBuffer<int> _Indices;

StructuredBuffer<float2> _Uvs;
Texture2D<float4> _CubeTexture;
SamplerState sampler_CubeTexture;


float rand()
{
    float result = frac(sin(_Seed / 100.0f * dot(_Pixel, float2(12.9898f, 78.233f))) * 43758.5453f);
    _Seed += 1.0f;
    return result;
}

// Random value in normal distribution (with mean=0 and sd=1)
float RandomValueNormalDistribution()
{
    // Thanks to https://stackoverflow.com/a/6178290
    float theta = 2 * 3.1415926 * rand();
    float rho = sqrt(-2 * log(rand()));
    return rho * cos(theta);
}

// Calculate a random direction
float3 RandomDirection()
{
    // Thanks to https://math.stackexchange.com/a/1585996
    float x = RandomValueNormalDistribution();
    float y = RandomValueNormalDistribution();
    float z = RandomValueNormalDistribution();
    return normalize(float3(x, y, z));
}

float3 RandomHemisphereDirection(float3 normal)
{
    float3 dir = RandomDirection();
    return dir * sign(dot(normal, dir));
}

float3x3 GetTangentSpace(float3 normal)
{
    float3 helper = float3(1, 0, 0);
    if (abs(normal.x) > 0.99f)
    {
        helper = float3(0, 0, 1);
    }
    float3 tangent = normalize(cross(normal, helper));
    float3 binormal = normalize(cross(normal, tangent));
    return float3x3(tangent, binormal, normal);
}

float3 SampleHemisphere(float3 normal, float alpha)
{
    // Sample the hemisphere, where the alpha determines the kind of the sampling
	// Uniform: 1.0f
	// Cosine:  0.5f
	// Phong:   1.0f / (alpha + 1.0f)

    float cosTheta = pow(rand(), 1 / (alpha + 1));
    float sinTheta = sqrt(max(0, 1 - cosTheta * cosTheta));
    float phi = 2 * PI * rand();
    float3 tangentSpaceDir = float3(cos(phi) * sinTheta, sin(phi) * sinTheta, cosTheta);
    return mul(tangentSpaceDir, GetTangentSpace(normal));
}

void IntersectGroundPlane(Ray ray, inout RayHit hit)
{
    float t = -ray.origin.y / ray.dir.y;
    if (t > 0 && t < hit.dist)
    {
        hit.dist = t;
        hit.pos = ray.origin + t * ray.dir;
        hit.normal = float3(0, 1, 0);
        hit.material.albedo = 0.5f;
        hit.material.specular = 0.03f;
        hit.material.smoothness = 0.2;
        hit.material.emission = float3(0, 0, 0);
    }
}

void IntersectSphere(Ray ray, inout RayHit hit, Sphere sphere)
{
    float3 d = ray.origin - sphere.pos;
    float p1 = -dot(ray.dir, d);
    float p2sqr = p1 * p1 - dot(d, d) + sphere.radius * sphere.radius;
    if (p2sqr < 0)
    {
        return;
    }

    float p2 = sqrt(p2sqr);
    float t = p1 - p2 > 0 ? p1 - p2 : p1 + p2;
    if (t > 0 && t < hit.dist)
    {
        hit.dist = t;
        hit.pos = ray.origin + t * ray.dir;
        hit.normal = normalize(hit.pos - sphere.pos);
        hit.material = sphere.material;
    }
}

bool IntersectTriangle_MT97(Ray ray, float3 vert0, float3 vert1, float3 vert2,
    inout float t, inout float u, inout float v)
{
    float3 edge1 = vert1 - vert0;
    float3 edge2 = vert2 - vert0;

    float3 pvec = cross(ray.dir, edge2);
    float det = dot(edge1, pvec);
    if (det < EPSILON)
    {
        return false;
    }

    float inv_det = 1 / det;
    float3 tvec = ray.origin - vert0;
    u = dot(tvec, pvec) * inv_det;
    if (u < 0 || u > 1)
    {
        return false;
    }

    float3 qvec = cross(tvec, edge1);
    v = dot(ray.dir, qvec) * inv_det;
    if (v < 0 || u + v > 1)
    {
        return false;
    }

    t = dot(edge2, qvec) * inv_det;
    return true;
}

void IntersectMeshObject(Ray ray, inout RayHit hit, MeshObject mesh)
{
    uint offset = mesh.indicesOffset;
    uint count = offset + mesh.indicesCount;
    for (uint i = offset; i < count; i += 3)
    {
        float3 v0 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i]], 1))).xyz;
        float3 v1 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i + 1]], 1))).xyz;
        float3 v2 = (mul(mesh.localToWorldMatrix, float4(_Vertices[_Indices[i + 2]], 1))).xyz;

        float t, u, v;
        if (IntersectTriangle_MT97(ray, v0, v1, v2, t, u, v))
        {
            if (t > 0 && t < hit.dist)
            {
                hit.dist = t;
                hit.pos = ray.origin + t * ray.dir;
                hit.normal = normalize(cross(v1 - v0, v2 - v0));
                hit.material = mesh.material;

                hit.uv = u * _Uvs[_Indices[i]] + v * _Uvs[_Indices[i + 1]] + (1 - u - v) * _Uvs[_Indices[i + 2]];
            }
        }
    }
}

float2 mod2(float2 x, float2 y)
{
    return x - y * floor(x / y);
}

float sdot(float3 x, float3 y, float f = 1.0f)
{
    return saturate(dot(x, y) * f);
}

float energy(float3 color)
{
    return dot(color, 1.0f / 3.0f);
}

float SmoothnessToPhongAlpha(float s)
{
    return pow(1000.0f, s * s);
}

RayHit Trace(Ray ray)
{
    RayHit hit = CreateRayHit();
    uint count, stride;
    //IntersectGroundPlane(ray, hit);

    _Spheres.GetDimensions(count, stride);
    for (uint i = 0; i < count; ++i)
    {
        IntersectSphere(ray, hit, _Spheres[i]);
    }

    _MeshObjects.GetDimensions(count, stride);
    for (uint i = 0; i < count; ++i)
    {
        IntersectMeshObject(ray, hit, _MeshObjects[i]);
    }

    return hit;
}

float3 Shade(inout Ray ray)
{
    float3 result = float3(0, 0, 0);

    for (int i = 0; i < 8; ++i)
    {
        RayHit hit = Trace(ray);

        if (hit.dist < 1.#INF)
        {
            Material material = hit.material;

            if (material.flag == CheckerPattern)
            {
                float2 c = mod2(floor(hit.pos.xz), 2.0);
                material.albedo = c.x == c.y ? material.albedo : material.specular;
            }
            else if (material.flag == HasTexture)
            {
                material.albedo = _CubeTexture.SampleLevel(sampler_CubeTexture, hit.uv, 0).xyz;
            }
            else if (material.flag == Invisible && i == 0)
            {
                ray.origin = hit.pos + ray.dir * 0.001;
                continue;
            }

            ray.origin = hit.pos + hit.normal * 0.001;
            const float3 diffuseDir = normalize(hit.normal + RandomDirection());
            const float3 specularDir = reflect(ray.dir, hit.normal);
            const bool isSpecularBounce = material.specularChance >= rand();
            ray.dir = lerp(diffuseDir, specularDir, material.smoothness * isSpecularBounce);

            const float3 emittedLight = material.emission * material.emissionStrength;
            result += emittedLight * ray.energy;
            ray.energy *= lerp(material.albedo, material.specular, isSpecularBounce);
        }
        else
        {
            ray.energy = 0;
            float theta = acos(ray.dir.y) / -PI;
            float pi = atan2(ray.dir.x, -ray.dir.z) / -PI * 0.5;
            //result += _SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(pi, theta), 0).xyz * 0.15;
        }

        if (!any(ray.energy))
        {
            break;
        }
    }

    return result;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    _Pixel = id.xy;

    uint width, height;
    Result.GetDimensions(width, height);

    float2 uv = float2((id.xy + _PixelOffset) / float2(width, height) * 2 - 1);
    Ray ray = CreateCameraRay(uv, _CameraToWorld, _CameraInverseProjection);

    float3 result = Shade(ray);
    Result[id.xy] = float4(result, 1);
}
